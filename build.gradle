buildscript {
    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/public/" }
    }
}

plugins {
    id 'java'
    // https://spring.io/projects/spring-boot
    id 'org.springframework.boot' version '3.3.4'
    // https://github.com/spring-gradle-plugins/dependency-management-plugin
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'net.hifor.demo'
version = 'main-20240829'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

ext {
    ver_fastjson2 = '2.0.53'
    ver_commonsLang3 = '3.17.0'
    ver_commonsIo = '2.17.0'
    ver_mybatisBoot = '3.0.3'
    ver_druidBoot = '1.2.23'
    ver_sqlserver = '12.8.1.jre11'
}

repositories {
    mavenLocal()
    maven { url "https://maven.aliyun.com/repository/public/" }
    mavenCentral()
}

dependencies {
    // ---- 基础框架
    // https://spring.io/projects/spring-boot
    implementation ('org.springframework.boot:spring-boot-starter-web'){
        // 排除自带的json解析器
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-json'
    }

    // ---- 工具包
    implementation "org.apache.commons:commons-lang3:${ver_commonsLang3}"
    implementation "commons-io:commons-io:${ver_commonsIo}"

    // json数据解析 https://github.com/alibaba/fastjson2
    implementation "com.alibaba.fastjson2:fastjson2:${ver_fastjson2}"
    implementation "com.alibaba.fastjson2:fastjson2-extension-spring6:${ver_fastjson2}"

    // ---- log4j2 日志
    // - Spring Boot: Configure Log4j for Logging
    //  https://docs.spring.io/spring-boot/how-to/logging.html#howto.logging.log4j.composite-configuration
    //- Log4j: Spring Boot Support
    //  https://logging.apache.org/log4j/2.x/log4j-spring-boot.html
    //- Log4j: Asynchronous Loggers for Low-Latency Logging
    //  https://logging.apache.org/log4j/2.x/manual/async.html
    implementation ('org.springframework.boot:spring-boot-starter-log4j2')
    modules {
        module("org.springframework.boot:spring-boot-starter-logging") {
            replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
        }
    }
    implementation 'com.lmax:disruptor:3.4.4'

    // ---- 数据库
    // 阿里数据库连接池 https://github.com/alibaba/druid
    // https://github.com/alibaba/druid/tree/master/druid-spring-boot-starter
    implementation "com.alibaba:druid-spring-boot-3-starter:${ver_druidBoot}"
    // https://mybatis.org/spring-boot-starter/mybatis-spring-boot-autoconfigure/index.html
    // https://github.com/mybatis/spring-boot-starter
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:${ver_mybatisBoot}"
    // 微软数据库驱动
    implementation "com.microsoft.sqlserver:mssql-jdbc:${ver_sqlserver}"


    // 单元测试
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

jar {
    from('src/main/java') {
        include '**/*.xml'
    }
}

bootJar {
    exclude('static/favicon.ico')
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDirs = ['src/main/java', 'src/main/resources']
        }
    }
}

test {
    useJUnitPlatform()
}
